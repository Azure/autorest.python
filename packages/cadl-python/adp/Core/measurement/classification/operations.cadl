using Cadl.Http;
using Cadl.Rest;
using Azure.Core;
using Azure.Core.Foundations.Extensions;
using OpenAPI;
using Autorest;

namespace Adp.Core.Measurement.Classification;

@tag("Measurement Classification")
interface MeasurementClassificationOperations {
  @doc("Get measurement classification by schema name.")
  get is ResourceRead<MeasurementClassification>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Assigns classification to the measurement.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperationStatus)
  create is CustomLongRunningResourceCreateUsingHttpPost<
    MeasurementClassification,
    CustomBodyWrapper<MeasurementClassificationCreationParameters>
  >;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Unassign the classification from the measurement.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperationStatus)
  delete is CustomLongRunningResourceDelete<MeasurementClassification>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Lists the classifications assigned to the measurement.")
  getAll is CustomResourceList<MeasurementClassification>;
}
