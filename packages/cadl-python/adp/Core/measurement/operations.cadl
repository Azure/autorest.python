using Cadl.Http;
using Cadl.Rest;
using Azure.Core;
using Azure.Core.Foundations.Extensions;
using OpenAPI;
using Autorest;

namespace Adp.Core.Measurement;

@tag("Measurement")
interface MeasurementOperations {
  @doc("Get measurement by ID.")
  get is ResourceRead<Measurement>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Deletes the measurement.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperationStatus)
  delete is CustomLongRunningResourceDelete<Measurement>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Lists the measurements in a workspace.")
  getAll is CustomResourceList<Measurement>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Lists the measurements in a workspace with extended metadata.")
  queryMeasurementsWithMetadata is ResourceCollectionAction<
    Measurement,
    {},
    Page<MeasurementWithMetadata>
  >;

  @doc("Lists the measurements in a workspace that are in the given measurement IDs list.")
  findByIds is ResourceCollectionAction<
    Measurement,
    CustomBodyWrapper<MeasurementListRequestParameters>,
    Page<Measurement>
  >;
}

@tag("Measurement")
interface MeasurementMetadataOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom resource list operation."
  @doc("Returns the measurement metadata.")
  @segmentOf(MeasurementMetadata)
  get is CustomSingletonResourceRead<
    MeasurementMetadata,
    {},
    MeasurementMetadataBase
  >;
}

@tag("Measurement")
interface MeasurementProcessingResultsOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom resource list operation."
  @doc("Returns the measurement processing result.")
  @segmentOf(MeasurementProcessingResults)
  get is CustomSingletonResourceRead<
    MeasurementProcessingResults,
    {},
    MeasurementProcessingResultsBase
  >;
}

@tag("Measurement")
interface MeasurementStateMachineOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom resource list operation."
  @doc("Returns the state machine instance for the measurement.")
  get is ResourceRead<StateMachine>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom resource list operation."
  @doc("List state machines instance for the measurement.")
  getAll is CustomResourceList<StateMachine>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Initiates process of applying an action on the measurement.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperationStatus)
  act is CustomLongRunningResourceAction<
    StateMachine,
    CustomBodyWrapper<StateMachineAction>
  >;
}

@tag("Measurement")
interface MeasurementMetadataFileInfoOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Initiates a process that replaces the measurement's metadata file")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperationStatus)
  complete is CustomLongRunningResourceCollectionAction<
    MeasurementMetadataFileInfo,
    CustomBodyWrapper<CompleteUploadMetadataFileRequest>,
    MeasurementMetadataFileInfoBase
  >;

  @doc("""
  Returns SAS signed URI that allows uploading temporary measurement metadata file to Azure Storage.
  This URI expires in 24 hours.
  """)
  getWritableUri is ResourceCollectionAction<
    MeasurementMetadataFileInfo,
    {},
    MeasurementMetadataFileInfoBase
  >;
}

@tag("Measurement")
interface MeasurementMetadataSchemaFileInfoOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom resource list operation."
  @doc("Returns the measurement metadata schema file information.")
  @segmentOf(MeasurementMetadataSchemaFileInfo)
  get is CustomSingletonResourceRead<
    MeasurementMetadataSchemaFileInfo,
    {},
    MeasurementMetadataSchemaFileInfoBase
  >;
}
