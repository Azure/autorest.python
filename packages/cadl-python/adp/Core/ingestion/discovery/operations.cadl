using Cadl.Http;
using Cadl.Rest;
using Azure.Core;
using Azure.Core.Foundations.Extensions;
using OpenAPI;
using Autorest;

namespace Adp.Core.Ingestion.Discovery;

@tag("Discovery")
interface DiscoveryOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom operation."
  @doc("Creates a new ingestion discovery instance.")
  createOrReplace is CustomResourceCreateOrReplace<
    Discovery,
    CustomBodyWrapper<DiscoveryCreationParameters>
  >;

  @doc("Get discovery by ID.")
  get is ResourceRead<Discovery>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom LRO response template the LRO actions."
  @doc("Initiates the process of completing the discovery and creating the upload file grouping manifest files.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperationStatus)
  complete is CustomLongRunningResourceAction<Discovery, {}>;

  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom LRO response template the LRO actions."
  @doc("Initiates the process of cancelling the discovery.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperationStatus)
  cancel is CustomLongRunningResourceAction<Discovery, {}>;
}

@tag("Discovery")
interface DiscoverySpecialFileOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom LRO response template the LRO actions."
  @doc("Initiates the process of generating SAS signed URIs for uploading special files for the discovery.")
  @extension("x-ms-long-running-operation", true)
  @asyncOperationOptions("location")
  @pollingOperation(LongRunningOperationStatus)
  generate is CustomLongRunningResourceCollectionAction<
    DiscoverySpecialFile,
    {},
    DiscoverySpecialFile
  >;

  @doc("""
  List special files details for the discovery resource.
  Returns SAS signed URI that allows uploading special files to Azure Storage.
  This URI expires in 24 hours.
  """)
  listWritableUris is ResourceCollectionAction<
    DiscoverySpecialFile,
    {},
    Page<DiscoverySpecialFile>
  >;
}

@tag("Discovery")
interface DiscoveryResultUploadOperations {
  #suppress "@azure-tools/cadl-azure-core/use-standard-operations" "ADP data-plane API uses custom resource list operation."
  @doc("List upload detail for the discovery resource.")
  list is CustomResourceList<DiscoveryUpload>;
}
